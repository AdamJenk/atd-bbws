/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'wiley' at '2/11/14 9:16 PM' with Gradle 1.10
 *
 */
task wrapper(type: Wrapper) {
    gradleVersion = "2.1"
}

apply plugin: 'java'
apply plugin: 'war'

if (isBuildingBlock()) {
    version = getB2Version()
}

repositories {
    mavenCentral()
    maven {
        url "https://bbprepo.blackboard.com/content/repositories/releases/"
    }
}

configurations {
    b2deploy
}

dependencies {
    b2deploy 'org.oscelot:b2deploy-task:0.1.0'

    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:jul-to-slf4j:1.7.5'
    compile 'org.slf4j:slf4j-simple:1.7.5'    

    compile 'net.sourceforge.stripes:stripes:1.5.7'
    compile 'org.springframework:spring-beans:3.2.5.RELEASE'
    compile 'org.springframework:spring-web:3.2.5.RELEASE'    

    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'blackboard.platform:bb-platform:9.1.100401.0'
    providedCompile 'blackboard.platform:bb-taglibs:9.1.100401.0'
    
    testCompile "junit:junit:4.11"
}

ant.taskdef(name: 'b2deploy', classname: 'org.oscelot.ant.B2DeployTask', classpath: configurations.b2deploy.asPath)

task deployb2(dependsOn: 'war') << {
    println "Deploying \"" + war.archivePath + "\""
    ant.b2deploy(localfilepath: war.archivePath,
        host: 'localhost:8081',
        clean: 'true',
        courseorgavailable:'true')
}


String getB2Version() {
  File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
  def manifest = new XmlSlurper().parse(mfFile);
  return manifest.plugin.version['@value'];
}

boolean isBuildingBlock() {
  File mfFile = new File(file(webAppDir), 'WEB-INF/bb-manifest.xml');
  return mfFile.exists();
}